
	;
	;	HSP help index build script
	;		onitama/onion software 1999
	;
	;#include "hspda.as"			; Use Data-Access DLL
	#include "mod_hs.as"
	#packopt name "mkindex"
	;
	sdim cb_group,512
	cb_group={"命令概要
プリプロセッサ命令
代入命令
特殊代入命令
プログラム制御命令
プログラム制御マクロ
基本入出力制御命令
拡張入出力制御命令
オブジェクト制御命令
画面制御命令
拡張画面制御命令
文字列操作命令
メモリ管理命令
マルチメディア制御命令
ファイル操作命令
拡張ファイル操作命令
通信制御命令
OSシステム制御命令
HSPシステム制御命令
その他の命令
"}
	;
	tt="HSP3 help index builder ver0.8"
	title tt
	;
	wx=640:wy=480
	screen 0,wx,wy
	gsel 0,1

	;	hsphelpフォルダに移動する
	s1=dir_exe
	chdir s1
	dirlist s2,"hsphelp",5
	if stat {
		chdir "hsphelp"
	}

	sdim ih_msg,$50000			; メッセージ表示バッファ(320K)
	sdim ih_list,$10000			; リスト表示バッファ(64K)

	cls 1
	objmode 1
	ih_msg=tt
	ih_msg+="/ ONION software 1999-2020(c)\n"
	ih_lsel=0
	;
	sysfont 17
	pos 2,wy-292
	mesbox ih_msg,wx-6,286,1		; ID0
	pos 2,0
	objsize 300,24
	listbox ih_lsel,160,ih_list		; ID1

	objsize 120,26
	y=4
	pos wx-330,y
	button "keyword表示",*main3
	pos wx-130,y
	button "index作成",*main2
	pos wx-130,y+146
	button "終了",*owari

*main0
	ihelp_find ""
	ih_list = ih_ansbuf
	notesel ih_list
	i=notemax
	ih_msg+="Total "+i+" keywords.\n"

	objprm 0,ih_msg
	objprm 1,ih_list
*main
	stop

*main2
	;	index作成
	;
	gosub *mkidx
	goto *main0

*main3
	;	keyword表示
	;
	sdim ln,256
	if ih_lsel<0 : goto *main	
	notesel ih_list
	noteget ln,ih_lsel
	if ln="" : goto *main

	ihelp_find ""
	ihelp_open ih_lsel

	ih_msg="["+ih_ans_key+"]   "+ih_ans_title+"\n"

	if ih_fncprm="" : goto *nofnc
	ih_msg += ih_ans_key+" "+ih_fncprm+"\n"
	ih_msg += ih_prminf+"\n"
	if ih_ans_dll!="" : ih_msg+="("+ih_ans_dll+"プラグイン/モジュールが必要です)\n"
*nofnc
	ih_msg+="\n"+ih_info+"\n"
	;
	if ih_prgsmp!="" {
		ih_msg+="\n[sample]\n"+ih_prgsmp+"\n"
	}
	;
	objprm 0,ih_msg
	goto *main



*mkidx
	;	idxファイル再構築main
	;
	ih_msg="idxファイルの更新中...\n"

	fname2="hsphelp.idx"			; 書き出しファイル
	sdim tidx1,$10000			; idx一時バッファ(64K)
	sdim tidx2,$10000			; idx一時バッファ(64K)
	sdim wrt,$18000				; idx書き出しバッファ(96K)
	sdim fl,$4000

	dirlist fl,"*.hs",1
	notesel fl
	flmax=notemax 
	if flmax=0 : goto *main2x
	flnum=0
*lp2
	;	"*.hs"をすべて解析
	;
	notesel fl
	noteget fname1,flnum
	gosub *docstart

	flnum++
	if flnum<flmax : goto *lp2

	;	tidxをソートしてwrtを作成
	;
	sdim ln,256
	sortnote tidx1
	;
	notesel tidx1
	i=notemax
	c=0
	wrt=""
	repeat i
	  notesel tidx1
	  noteget ln,cnt
	  c=peek(ln,0)
	  if c=0 : goto *tchkov
	  wrt+=ln+"\n"
	  sortget c,cnt
	  notesel tidx2
	  noteget ln,c
	  wrt+=ln+"\n"
*tchkov
	loop

	;	idxファイルをセーブ
	;
	notesel wrt
	notesave fname2
	ih_msg+="["+fname2+"] セーブしました.\n"

*main2x
	ih_msg+=fname2+"を再構築しました.\n"
	return


*docstart
	;	hsファイルを解析してidxを作成
	;
	exist fname1
	if strsize<0 : dialog "fatal error" : end
	sdim buf,strsize+4			; hs読み込みバッファ

	sdim a,256
	sdim ln,256
	sdim dllname,64

	ih_msg+="file ["+fname1+"] を読み込みます.\n"
	bload fname1,buf
	bufmax=strlen(buf)

	i=0:idn=0
	c=0
*doc1
	idn=i					; 行IDを保存
	getstr ln,buf,i,0:i+=strsize
	c=peek(ln,0)
	if c!=$25 : goto *doc2			; '%' check
	;
	a=strmid(ln,1,64)
	if a="dll" : goto *doc3			; "%dll" check
	if a!="index" : goto *doc2		; "%index" check
	;
	getstr a,buf,i,0:i+=strsize		; 次の行を取得
	getstr ln,buf,i,0:i+=strsize		; その次の行を取得
	if i>=bufmax : goto *doc2		; error check
	;
	tidx1+=a+"\n"
	tidx2+=""+idn+","+fname1+","+dllname+","+ln+"\n"
*doc2
	if i<bufmax : goto *doc1
	return
*doc3
	getstr dllname,buf,i,0:i+=strsize	; 次の行を取得
	if dllname="none" : dllname=""
	goto *doc2

*owari
	end

